plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 29

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "com.bailey.rod.cbaexercise"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        all {
            // Set UseLocalData to false for your chosen build flavor to use remote account
            // activity summary data rather than the local data in SampleAccountDataFile
            buildConfigField "String", "ServiceBaseUrl", "\"https://www.dropbox.com/\""
            buildConfigField "long", "ConnectTimeoutMinutes", "1L"
            buildConfigField "long", "ReadTimeoutMinutes", "1L"
            buildConfigField "long", "WriteTimeoutMinutes", "2L"
            buildConfigField "String", "CbaServiceKey", "\"tewg9b71x0wrou9\""
            buildConfigField "String", "CbaServiceFileName", "\"data.json\""
            buildConfigField "int", "CbaServiceDownloadFlag", "1"

            // Set UseLocalData to true for your chosen build flavor to use the account activity
            // summary data in SampleAccountDataFile
            buildConfigField "String", "SampleAccountDataFile", "\"sample_account_data.json\""
            buildConfigField "boolean", "UseLocalData", "false"

            buildConfigField "float", "InitialMapZoomLevel", "16"

        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

}

dependencies {

    // Google' GSON library is used for de-serialising JSON data
    implementation "com.google.code.gson:gson:$gson_version"

    // 'Retrofit' is for RESTful network communications.
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$ok_http_version"

    // Only included to prevent compilation error
    // https://stackoverflow.com/questions/58630667/rejecting-re-init-on-previously-failed-class-in-okhttp/58662077#58662077
    implementation "org.conscrypt:conscrypt-android:2.2.1"

    // Pull-to-refresh UI component
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipe_version"

    // "Timber" logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // View models
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'

    // Dagger 2
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation "com.google.dagger:dagger:$dagger_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
}